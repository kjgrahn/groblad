#!/usr/bin/perl -w
#
# kärlvl - extract straight taxon lists from dyntaxa/*, in turn
#          extracted and converted from <https://www.dyntaxa.se>.
# 
# Copyright (c) 2012 Jörgen Grahn
# All rights reserved.
#
# usage: kärlvl file ...
#
#

use strict;
use Getopt::Std;


sub ssp($) {
    my ($s) = @_;
    $s =~ s/subsp\./ssp./g;
    return $s;
}


# Perform the conversion for one file; print to stdout
#
# The columns unfortunately have different meanings in
# e.g. 'Lycopodiophyta' and 'Magnoliophyta'.  So we have to read the
# headings (the first row) before we can process the taxa.
#
# Also unfortunately, some columns are duplicated with the same name
# but different meanings. E.g. 'Art' can be the latin species name if
# the taxon is a species, but a second copy of the column can be the
# latin species name above a ssp, form, and so on.
# Solved by ignoring duplicate columns encountered.
#
# The result is:
#
# 'Familj' - turned into a section heading
#
# 'Artkomplex'
# 'Sektion'
# 'Art'
# 'Underart'
# 'Varietet'
# 'Hybrid' - turned into the Latin name
#
# 'Svenskt namn' - used if it exists
#
sub convert($$$) {
    my ($justhyb, $nohyb, $file) = @_;

    my %headings;

    open(F, "<$file") or die;
    while(<F>) {
	chomp;

	last if /^@/ and %headings;
	next if /^@/;
	/^(\d+)\s+=\s+(.+)/ or die;
	$headings{$2} = $1 unless defined $headings{$2};
    }

    my $header = $headings{'Familj'} or die;
    my $sv = $headings{'Svenskt namn'} or die;
    my @la;

    # Which taxonomic levels to include.
    my @levels = ('Artkomplex',
		  'Sektion',
		  'Art',
		  'Underart',
		  'Varietet');
    @levels = () if $justhyb;
    push @levels, 'Hybrid' unless $nohyb;

    foreach (@levels) {
	push @la, $headings{$_} if defined $headings{$_};
    }

    #print @la; print "\n"; return;

    my %CLEAR = ($sv => '-');
    my %row;

    while(<F>) {
	chomp;

	if(/^@/) {

	    next unless %row;

	    if(defined $row{$header}) {
		printf "\n# %s\n", $row{$header};
	    }

	    foreach (@la) {
		next unless defined $row{$_};
		printf "%-23s (%s)\n", $row{$sv}, $row{$_};
		last;
	    }

	    %row = %CLEAR;
	    next;
	}

	/^(\d+)\s+=\s+(.+)/ or die;
	$row{$1} = ssp $2;
    }

    if(%row) {
	foreach (@la) {
	    next unless defined $row{$_};
	    printf "%-23s (%s)\n", $row{$sv}, $row{$_};
	    last;
	}
    }

    close F;
}


my %opts;
getopts('hH', \%opts) or die "usage: $0 -[Hh] file ...\n";

foreach (@ARGV) {
    convert(defined $opts{'h'},
	    defined $opts{'H'},
	    $_);
}

__END__
