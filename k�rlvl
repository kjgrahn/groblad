#!/usr/bin/perl -w
#
# kärlvl - extract straight taxon lists from dyntaxa/*, in turn
#          extracted and converted from <https://www.dyntaxa.se>.
# 
# Copyright (c) 2012, 2013 Jörgen Grahn
# All rights reserved.
#
# usage: kärlvl file ...
#
#

use strict;
use Getopt::Std;


sub ssp($) {
    my ($s) = @_;
    $s =~ s/subsp\./ssp./g;
    return $s;
}


# Perform the conversion for one file; print to stdout.
# Assumes certain columns exist, most importantly
# 'Taxonkategori' and 'Vetenskapligt namn'.
#
sub convert($) {
    my ($file) = @_;

    my %headings;

    open(F, "<$file") or die;
    while(<F>) {
	chomp;

	last if /^@/ and %headings;
	next if /^@/;
	/^(\d+)\s+=\s+(.+)/ or die;
	$headings{$2} = $1 unless defined $headings{$2};
    }

    my $id = $headings{'TaxonId'};
    my $la = $headings{'Vetenskapligt namn'} or die;
    my $sv = $headings{'Svenskt namn'} or die;
    my $level = $headings{'Taxonkategori'} or die;

    # Which taxonomic levels (categories) to include.
    my %taxon_levels = ('Sektion' => 1,
			'Art' => 1,
			'Underart' => 1,
			'Varietet' => 1,
			'Form' => 1,
			'Hybrid' => 1);

    my %CLEAR;
    my %row;

    while(<F>) {
	chomp;

	if(/^@/) {

	    next unless %row;

	    $row{$sv} = '-' unless defined $row{$sv};

	    if($row{$level} eq 'Familj') {
		printf "\n# %s\n", $row{$la};
	    }
	    elsif(defined $taxon_levels{$row{$level}}) {
		printf("%-23s (%s)\n",
		       $row{$sv},
		       $row{$la});
	    }

	    %row = %CLEAR;
	    next;
	}

	/^(\d+)\s+=\s+(.+)/ or die;
	$row{$1} = ssp $2;
    }

    if(%row and defined $taxon_levels{$row{$level}}) {
	$row{$sv} = '-' unless defined $row{$sv};
	printf("%-23s (%s)\n",
	       $row{$sv},
	       $row{$la});
    }

    close F;
}


my %opts;
getopts('', \%opts) or die "usage: $0 file ...\n";

foreach (@ARGV) {
    convert($_);
}

__END__
